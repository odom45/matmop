plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'com.matmop.app'
    compileSdk 34

    defaultConfig {
        applicationId "com.matmop.app"
        minSdk 21
        targetSdk 34
        versionCode 3
        versionName "1.0.2"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }

    // Conditional signing config for Play upload. If RELEASE_* properties are provided,
    // the release bundle will be signed; otherwise it builds unsigned (still valid for testing/CI artifact).
    def hasSigning = project.hasProperty('RELEASE_STORE_FILE') &&
            project.hasProperty('RELEASE_STORE_PASSWORD') &&
            project.hasProperty('RELEASE_KEY_ALIAS') &&
            project.hasProperty('RELEASE_KEY_PASSWORD')

    signingConfigs {
        release {
            if (hasSigning) {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasSigning) {
                signingConfig signingConfigs.release
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
            version "3.18.1"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.20"
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'org.commonmark:commonmark:0.21.0'
    implementation 'org.commonmark:commonmark-ext-gfm-tables:0.21.0'
}

